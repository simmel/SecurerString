apply plugin: 'java'
apply plugin: 'maven-publish'
defaultTasks 'build'

repositories.mavenCentral()

group = "se.soy.${project.name.toLowerCase()}"
version = "1.0"

dependencies.testCompile 'junit:junit:4.+'

test {
    systemProperty "temporaryDir", "${temporaryDir}"
    reports.junitXml.enabled = false
    reports.html.enabled = false
    forkEvery 1
    maxParallelForks Runtime.getRuntime().availableProcessors();
}

task sourceJar (type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar (type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

publishing {
  publications {
    jar(MavenPublication) {
      from components.java

      artifact (sourceJar) {
        classifier = 'sources'
      }
      artifact (javadocJar) {
        classifier = 'javadoc'
      }
    }
  }
}

task publishToMavenLocal.doLast {
  println new File("build/publications/SecurerString/pom-default.xml").text
}

jar.manifest.attributes 'Main-Class': "${group}.${project.name}"
