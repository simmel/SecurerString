apply plugin: 'java'
apply plugin: 'maven-publish'
apply from: 'mvn-sign-deploy.gradle'
defaultTasks 'build'

repositories.mavenCentral()

group = "se.soy.${project.name.toLowerCase()}"
version = "1.2.0-SNAPSHOT"
ext {
  url = "https://github.com/simmel/${project.name}"
  gitRepo = "git@github.com:simmel/${project.name}.git"
}

dependencies.testCompile 'junit:junit:4.+'

test {
  systemProperty "temporaryDir", "${temporaryDir}"
  reports.junitXml.enabled = false
  reports.html.enabled = false
  forkEvery 1
  maxParallelForks Runtime.getRuntime().availableProcessors();
}

task checkJhat {
  def result = exec {
    commandLine "pgrep", "-f", "jhat -port"
    standardOutput new OutputStream() { @Override public void write(int b) { } };
    ignoreExitValue true
  }
  if (!result.getExitValue()) {
    logger.error('\033[0;31mERROR: Found jhat(s) running, this will produce erroneous test results!\033[0m')
    System.exit(1)
  }
}
test.dependsOn checkJhat

publishing {
  publications {
    jar(MavenPublication) {
      from components.java

      artifact (sourceJar) {
        classifier = 'sources'
      }
      artifact (javadocJar) {
        classifier = 'javadoc'
      }

      pom.withXml {
        asNode().children().last() + {
          resolveStrategy = Closure.DELEGATE_FIRST
          parent {
            resolveStrategy = Closure.DELEGATE_ONLY
            groupId 'org.sonatype.oss'
            artifactId 'oss-parent'
            version '9'
          }
          name project.name
          description 'a "String" library for shredding sensitive data after use'
          url url
          packaging 'jar'
          scm {
            url project.gitRepo
            connection "scm:${project.gitRepo}"
            developerConnection "scm:${project.gitRepo}"
          }
          licenses {
            license {
              name 'ISC license'
              url 'https://www.isc.org/downloads/software-support-policy/isc-license/'
              distribution 'repo'
            }
          }
          developers {
            developer {
              id 'simmel'
              name 'Simon Lundstr√∂m'
              email 'simmel@soy.se'
            }
          }

          build {
            plugins {
              plugin {
                resolveStrategy = Closure.DELEGATE_ONLY
                groupId 'org.apache.maven.plugins'
                artifactId 'maven-gpg-plugin'
                version '1.4'
                configuration {
                  useAgent 'true'
                  executable 'src/build/scripts/gpg'
                }
              }
            }
          }
        }
      }
    }
  }
}

jar.manifest.attributes 'Main-Class': "${group}.${project.name}"
