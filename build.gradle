apply plugin: 'java'
apply plugin: 'maven-publish'
defaultTasks 'build'

repositories.mavenCentral()

group = "se.soy.${project.name.toLowerCase()}"
version = "1.0"
ext {
  url = "https://github.com/simmel/${project.name}"
  gitRepo = "git@github.com:simmel/${project.name}.git"
  pomFile = file("$project.buildDir/pom.xml")
}

dependencies.testCompile 'junit:junit:4.+'

model {
  tasks.generatePomFileForJarPublication {
    destination = project.pomFile
  }
}

test {
    systemProperty "temporaryDir", "${temporaryDir}"
    reports.junitXml.enabled = false
    reports.html.enabled = false
    forkEvery 1
    maxParallelForks Runtime.getRuntime().availableProcessors();
}

task sourceJar (type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar (type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

task publishToMavenLocal.doLast {
  println pomFile.text
}

publishing {
  publications {
    jar(MavenPublication) {
      from components.java

      artifact (sourceJar) {
        classifier = 'sources'
      }
      artifact (javadocJar) {
        classifier = 'javadoc'
      }

      pom.withXml {
        asNode().children().last() + {
          resolveStrategy = Closure.DELEGATE_FIRST
          parent {
            resolveStrategy = Closure.DELEGATE_ONLY
            groupId 'org.sonatype.oss'
            artifactId 'oss-parent'
            version '7'
          }
          name project.name
          description 'a "String" library for shredding sensitive data after use'
          url url
          packaging 'jar'
          scm {
            url project.gitRepo
            connection "scm:${project.gitRepo}"
            developerConnection "scm:${project.gitRepo}"
          }
          licenses {
            license {
              name 'ISC license'
              url 'https://www.isc.org/downloads/software-support-policy/isc-license/'
              distribution 'repo'
            }
          }
          developers {
            developer {
              id 'simmel'
              name 'Simon Lundstr√∂m'
              email 'simmel@soy.se'
            }
          }
        }
      }
    }
  }
}

jar.manifest.attributes 'Main-Class': "${group}.${project.name}"
